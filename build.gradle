plugins {
  id 'com.jfrog.artifactory' version '4.8.1'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven-publish'
}

repositories {
  jcenter()
  google()
}

apply from: 'gradle/dependencies.gradle'

group = GROUP
version = VERSION_NAME
description = POM_DESCRIPTION

sourceCompatibility = versions.java
targetCompatibility = versions.java

jar {
  manifest {
    attributes(
      'Implementation-Title': POM_NAME,
      'Implementation-Version': version,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version'),
      'Built-Gradle': gradle.gradleVersion)
  }
}

dependencies {
  implementation localGroovy()

  testImplementation deps.android.tools.build.gradle
  testImplementation deps.spock, { exclude module: 'groovy-all' } // Use localGroovy()
}

apply from: 'gradle/compile.gradle'
apply from: 'gradle/plugin.gradle'
apply from: 'gradle/publish.gradle'
